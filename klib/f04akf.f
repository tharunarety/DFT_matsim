      SUBROUTINE F04AKF(N, IR, A, IA, RINT, B, IB)
C     MARK 2 RELEASE. NAG COPYRIGHT 1972
C     MARK 4.5 REVISED
C     MARK 11 REVISED. VECTORISATION (JAN 1984).
C
C     DOUBLE PRECISION VERSION WITH COMPLEX
C
C     COMPSOL
C     SOLVES AX=B, WHERE A IS A COMPLEX UNSYMMETRIC MATRIX AND B
C     IS A COMPLEX MATRIX OF IR RIGHT-HAND SIDES.
C     THE SUBROUTINE F04AKF MUST BE PROCEDED BY
C     F03AHF IN WHICH L AND U ARE PRODUCED IN THE ARRAY A(N,N),
C     FROM A, AND THE RECORD OF THE INTERCHANGES IS
C     PRODUCED IN RINT(N). AX=B IS SOLVED IN THREE STEPS,
C     INTERCHANGE ELEMENTS OF B, LY=B AND UX=Y. THE MATRICES Y AND
C     THEN X ARE OVERWRITTEN ON B.
C     1ST AUGUST 1971
C
      INTEGER I, N, I1, IR, K, IA, IB
      COMPLEX*16 A(IA,N), B(IB,IR), CX
      DOUBLE PRECISION RINT(N)
C     INTERCHANGE ELEMENTS OF B
      DO 40 I=1,N
         I1 = RINT(I) + 0.5D0
         IF (I1.EQ.I) GO TO 40
         DO 20 K=1,IR
            CX = B(I,K)
            B(I,K) = B(I1,K)
            B(I1,K) = CX
   20    CONTINUE
   40 CONTINUE
      DO 100 K=1,IR
C     SOLUTION OF LY= B
         CALL F03AHZ(A, IA, N, N, B(1,K))
C     SOLUTION OF UX= Y
         CALL F04AKZ(A, IA, N, B(1,K))
  100 CONTINUE
      RETURN
      END

      SUBROUTINE rotgf(g)
C   ******************************************************************
C   *                                                                *
C   *   Integrate the g matrix for the star of the k-point as:       *
C   *                                                                *
C   *     g(T(k)) = g(T(k)) + UGAM(T)*g(k)*UGAM^(-1)(T) , where      *
C   *                                                                *
C   *   UGAM are the rotation unitary matrices generated by ROTM3D.  *
C   *                                                                *
C   *   g = g_{Q,L',L} is a site diagonal matrix of L = {lm}.        *
C   *                                                                *
C   ******************************************************************
      USE control_data
      USE symmetry
      IMPLICIT NONE
      COMPLEX(KIND=8), DIMENSION(nq,nlm,nlm) :: g, ug, ugu
      COMPLEX(KIND=8) :: smm
      REAL(KIND=8) :: wq
      INTEGER :: iq, jq, l, m, lm, lmp, mpp, lmpp, irot
C
      ugu=zero
      DO 20 irot=1,nrot
      IF(iwrot(irot).NE.0) THEN
C
         DO 21 iq=1,nq
         DO 21 l=0,lmax
         DO 21 m=-l,l
         lm=l*l+l+m+1
C
         DO 22 lmp=1,nlm
         smm=0.d0
         DO 23 mpp=-l,l
         lmpp=l*l+l+mpp+1
   23    smm=smm+ugam(irot,l,m,mpp)*g(iq,lmpp,lmp)
   22    ug(iq,lm,lmp)=smm
C
   21    CONTINUE
C
         DO 24 iq=1,nq
         wq=wqst(iq)
         DO 24 l=0,lmax
         DO 24 m=-l,l
         lm=l*l+l+m+1
C
         jq=iprmt(irot,iq)
C
         DO 25 lmp=1,nlm
         smm=0.d0
         DO 26 mpp=-l,l
         lmpp=l*l+l+mpp+1
   26    smm=smm+ug(iq,lmp,lmpp)*ugam(irot,l,m,mpp)*wq
   25    ugu(jq,lmp,lm)=ugu(jq,lmp,lm)+smm
C
   24    CONTINUE
      ENDIF
   20 CONTINUE
      g=ugu
C
      RETURN
      END
